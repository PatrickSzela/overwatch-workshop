import '../../libs/menu/components/component.del'; 
import '../../libs/menu/components/componentWithValue.del'; 
import '../../libs/menu/components/text.del'; 
import '../../libs/menu/components/link.del'; 
import '../../libs/menu/components/numberInput.del'; 
import '../../libs/menu/components/toggle.del';
import '../../libs/menu/components/checkbox.del'; 
import '../../libs/menu/components/radio.del'; 
import '../../libs/menu/components/carousel.del'; 
import './_variables.del';
import './pages/_index.del';
import './pages2/index.del';
import '../../libs/utils/rules/pauseMatchTime.del';
import './customGameSettings.lobby';
import '../../libs/playerInfo.del';

// TODO: do not foreach array of players!!!

// TODO: teleport/look at flag
// TODO: menus: player speed, gravity, attach player, communicate, invisibility, walk towards, more here https://workshop.codes/wiki/categories/actions
// TODO: tools - ruler, spheres

Button SWITCH_FOCUS_BUTTON: Button.Ability1;
playervar Number switchFocusDescriptionIdx;

rule: 'Generate Menu State'
Event.OngoingPlayer
{
  menuState = new MenuState('root', [Button.Crouch, Button.Interact], EventPlayer() == HostPlayer());
  menuState.SetFocused();

  menuState2 = new MenuState(null, [], false, true);
  menuState2.AddMenuDescription(Description.New('\n', order: 9999));
}

rule: 'Generate Menu'
{
  DisableInspectorRecording();

  // TODO: add info about state being a variable of LocalPlayer
  menu = new Menu(LocalPlayer().menuState);
  menu2 = new Menu(LocalPlayer().menuState2, MenuLocation.Right);
}

rule: 'Hide diagnostics by default'
Event.OngoingPlayer
{
  diagnosticsEnabled = false;
}

rule: 'Switch focused menu'
Event.OngoingPlayer
if(currentMenuState.isOpen)
if(menuState.HistoryContains(PAGE_PLAYER_SETTINGS_ID))
if(EventPlayer().IsButtonHeld(SWITCH_FOCUS_BUTTON))
{
  if(currentMenuState == menuState) {
    menuState2.SetFocused();
  } else {
    menuState.SetFocused();
  }
}

rule: 'Close main menu when trying to do that in the 2nd one'
Event.OngoingPlayer
if(menuState2.isFocused)
if(menuState.HistoryContains(PAGE_PLAYER_SETTINGS_ID))
if(buttonsHeld == menuState._toggleMenu)
{
  if(menuState.isOpen)
    menuState.Close();
  else
    menuState.Open();
}

rule: 'Show description on currently focused menu'
Event.OngoingPlayer
if(currentMenuState.isFocused)
if(menuState.HistoryContains(PAGE_PLAYER_SETTINGS_ID))
{
  switchFocusDescriptionIdx = currentMenuState.AddMenuDescription(Description.New($'Press {ButtonFormatterSingle(SWITCH_FOCUS_BUTTON)}â€‹ to switch to other menu', COLOR_SWITCH, order: 99));
  currentMenuState.RerenderHoveredItem();

  MenuState cur! = currentMenuState;
  WaitUntil(cur != currentMenuState || !menuState.HistoryContains(PAGE_PLAYER_SETTINGS_ID), 99999);

  cur.RemoveMenuDescription(switchFocusDescriptionIdx);
  cur.RerenderHoveredItem();

  AbortIfConditionIsFalse();
  LoopIfConditionIsTrue();
}

rule: 'Open Select Players menu'
Event.OngoingPlayer
if(menuState.isOpen)
if(menuState.HistoryContains(PAGE_PLAYER_SETTINGS_ID))
{
  menuState2.SetEnabled(true);
  menuState2.Open();
  menuState2.GoTo(PAGE_SELECT_PLAYERS_ID);
  WaitUntil(!menuState.isOpen || !menuState.HistoryContains(PAGE_PLAYER_SETTINGS_ID), 99999);
  menuState2.SetEnabled(false);
}

rule: 'Player Info'
Event.OngoingPlayer
{
  PlayerInfo(playerName, buttonsHeld, pageStatusAppliedStatuses);
}
