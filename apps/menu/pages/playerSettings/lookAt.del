import '../../../../libs/menu/components/carousel.del';
import '../../../../libs/utils/string.formatters.del';
import '../../../../libs/utils/player.sortedAllPlayers.del';
import '../../_variables.del';

String PAGE_LOOK_AT_ID: 'lookAt';
String PAGE_LOOK_AT_NAME: 'Look At';

String PAGE_LOOK_AT_POSITION_ID: PAGE_LOOK_AT_ID + 'position';
String PAGE_LOOK_AT_POSITION_NAME: 'Position';
String PAGE_LOOK_AT_OBJECTIVE_ID: PAGE_LOOK_AT_ID + 'objective';
String PAGE_LOOK_AT_OBJECTIVE_NAME: 'Objective';
String PAGE_LOOK_AT_PAYLOAD_ID: PAGE_LOOK_AT_ID + 'payload';
String PAGE_LOOK_AT_PAYLOAD_NAME: 'Payload';
String PAGE_LOOK_AT_PLAYER_ID: PAGE_LOOK_AT_ID + 'player';
String PAGE_LOOK_AT_PLAYER_NAME: 'Player';
String PAGE_LOOK_AT_APPLY_ID: PAGE_LOOK_AT_ID + 'apply';
String PAGE_LOOK_AT_APPLY_NAME: 'Apply';
Button PAGE_LOOK_AT_BUTTON_APPLY_ONCE: Button.Ability2;
Button PAGE_LOOK_AT_BUTTON_APPLY_CONTINUOUSLY: Button.Reload;

String[] PAGE_LOOK_AT_DESCRIPTION: [
  $"Press {ButtonFormatterSingle(PAGE_LOOK_AT_BUTTON_APPLY_ONCE)}​ to apply once",
  $"Press {ButtonFormatterSingle(PAGE_LOOK_AT_BUTTON_APPLY_CONTINUOUSLY)}​ to apply continuously"
];

// TODO: look at looking position of player

Vector GetDirectionFromPlayerEyes(Player player, Vector vector): 
  player.EyePosition().DirectionTowards(vector);

Component[] PageLookAtItems() {
  Component position = new Component(PAGE_LOOK_AT_POSITION_NAME);
  position.onEnter = (it) => {
    pickPosition.Enable(Color.Red);
  };
  position.onLeave = (it) => {
    pickPosition.Disable();
  };
  position.onButtonDown = (button, it) => {
    pickPosition.ChangeDistance(button);

    if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_ONCE])
      PageLookAtApplyHelper(false, pickPosition.position);
    else if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_CONTINUOUSLY])
      PageLookAtApplyHelper(true, EvaluateOnce(pickPosition.position));
  };
  position.AddDescription(PickPosition.CHANGE_DISTANCE_DESCRIPTION);

  Carousel objective = new Carousel(
    text: PAGE_LOOK_AT_OBJECTIVE_NAME,
    options: [-1, 0, 1, 2, 3, 4]
  );
  objective.valueFormatter = (it) => ObjectiveFormatter(it.value);
  objective.onButtonDown = (button, it) => {
    if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_ONCE])
      PageLookAtApplyHelper(false, ObjectivePosition((<Carousel>it).value == -1 ? ObjectiveIndex() : (<Carousel>it).value));
    else if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_CONTINUOUSLY])
      PageLookAtApplyHelper(true, ObjectivePosition(EvaluateOnce((<Carousel>it).value) == -1 ? ObjectiveIndex() : EvaluateOnce((<Carousel>it).value)));
  };

  Component payload = new Component(PAGE_LOOK_AT_PAYLOAD_NAME);
   payload.onButtonDown = (button, it) => {
    if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_ONCE])
      PageLookAtApplyHelper(false, PayloadPosition());
    else if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_CONTINUOUSLY])
      PageLookAtApplyHelper(true, PayloadPosition());
  };

  Carousel player = new Carousel(
    text: PAGE_LOOK_AT_PLAYER_NAME,
    options: SortedAllPlayersFlat()
  );
  player.valueFormatter = (it) => PlayerWithHeroIconTeamSlotFormatter(it.value);
  player.onButtonDown = (button, it) => {
    if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_ONCE])
      PageLookAtApplyHelper(false, EyePosition((<Carousel>it).value));
    else if(buttonsHeld == [PAGE_LOOK_AT_BUTTON_APPLY_CONTINUOUSLY])
      PageLookAtApplyHelper(true, EyePosition(EvaluateOnce((<Carousel>it).value)));
  };

  return [
    position,
    objective,
    payload,
    player
  ];
}

rule: 'Player Settings/Look At'
Event.OngoingPlayer
if(menuState.pageId == PAGE_LOOK_AT_ID)
{
  Description desc1: { text: PAGE_LOOK_AT_DESCRIPTION[0], ..Description.BASE };
  Description desc2: { text: PAGE_LOOK_AT_DESCRIPTION[1], ..Description.BASE };

  menuState.CreatePage(PAGE_LOOK_AT_NAME, PageLookAtItems(), [desc1, desc2]);
}

rule: 'Player Settings/Look At: Create Sphere'
Event.OngoingPlayer
if(menuState.isOpen)
if(menuState.pageId == PAGE_LOOK_AT_ID)
{
  foreach(Player p! in AllPlayers()) {
    if(p == EventPlayer()) continue;

    pickPosition.CreateAdditionalSphere(
      color: Color.Green,
      position: PickPosition.RayCast(EvaluateOnce(p)),
      text: $"{EvaluateOnce(p)}'s look position:\n{PickPosition.RayCast(EvaluateOnce(p))}",
      lineEnd: EvaluateOnce(p).EyePosition()
    );

    Wait(0.05);
  }

  WaitUntil(menuState.pageId != PAGE_LOOK_AT_ID || !menuState.isOpen, 99999);

  pickPosition.DestroyAdditionalSpheres();
}

void PageLookAtApplyHelper(in Boolean continuously!, in Vector position!) {
  foreach(Player p! in selectedPlayers) {
    StopFacing(p);

    if(!continuously) {
      SetFacing(p, GetDirectionFromPlayerEyes(p, position), Relative.ToWorld);
    } else {
      // FIXME: figure out why this breaks after bot's death
      StartFacing(p, GetDirectionFromPlayerEyes(EvaluateOnce(p), position), 200, Relative.ToWorld, FacingRev.DirectionAndTurnRate);
    }
  }
}
