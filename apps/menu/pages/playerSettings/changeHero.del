import '../../../../libs/menu/components/carousel.del';
import '../../../../libs/utils/string.formatters.del';
import '../../_variables.del';

String PAGE_CHANGE_HERO_ID: 'changeHero';
String PAGE_CHANGE_HERO_NAME: 'Change Hero';

playervar Hero changeHero;

Component PageChangeHeroMenuItem() {
  Carousel carousel! = new Carousel(
    text: PAGE_CHANGE_HERO_NAME, 
    options: AllTankHeroes() + AllDamageHeroes() + AllSupportHeroes(),
    defaultValue: selectedPlayers.First.Hero()
  );

  carousel.valueFormatter = (it) => HeroWithIconFormatter(it.value);

  carousel.SetPrimaryAction("Apply", (it) => {
    selectedPlayers.changeHero = (<Carousel>it).value;
  });
  carousel.SetTertiaryAction("Add to timeline", (it) => {
    Timeline.AddAction(timeline, ACTION_ChangeHero(selectedPlayers, (<Carousel>it).value));
  });

  // based on https://workshop.codes/wiki/articles/send+a+player+back+to+hero+select
  // this won't work if Respawn as random hero is enabled
  carousel.SetSecondaryAction("Send to Hero Select screen", (it) => {
    StopForcingHero(selectedPlayers);

    foreach(Player pl! in selectedPlayers)
      SetAllowedHeroes(pl, AllHeroes().FilteredArray((i) => i != pl.Hero()));

    ResetHeroAvailability(selectedPlayers);
  });
  
  return carousel;
}

rule: 'Player Settings/Change Hero: Set hero (workaround for dummies)'
Event.OngoingPlayer
if(changeHero)
{
  WaitUntil(changeHero != EventPlayer().Hero(), 99999);
  ForcePlayerHero(EventPlayer(), changeHero);
  Loop();
}
