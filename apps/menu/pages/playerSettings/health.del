import '../../../../libs/menu/components/text.del';
import '../../../../libs/menu/components/toggle.del';
import '../../../../libs/menu/components/numberInput.del';
import '../../_variables.del';

String PAGE_HEALTH_ID: 'health';
String PAGE_HEALTH_NAME: 'Health';
String PAGE_HEALTH_CURRENT_HEALTH_ID: PAGE_HEALTH_ID + 'current';
String PAGE_HEALTH_CURRENT_HEALTH_NAME: 'Current Health';
Button PAGE_HEALTH_CURRENT_HEALTH_BUTTON_APPLY: Button.Ability2;
String PAGE_HEALTH_MAX_HEALTH_ID: PAGE_HEALTH_ID + 'max';
String PAGE_HEALTH_MAX_HEALTH_NAME: 'Max Health';
Button PAGE_HEALTH_MAX_HEALTH_BUTTON_APPLY: Button.Ability2;
Button PAGE_HEALTH_MAX_HEALTH_BUTTON_RESET: Button.Reload;

String PAGE_HEALTH_DESCRIPTION_INFO: "All players have Current and Max Health above their heads";

playervar Boolean pageHealthAutoHeal = false;

// TODO: health pool

Component[] PageHealthItems() {
  NumberInput currentHealth = new NumberInput(
    menuState: menuState, 
    text: PAGE_HEALTH_CURRENT_HEALTH_NAME,
    integralLength: 5,
    defaultValue: selectedPlayers.First.Health(),
    unit: ' HP'
  );
  currentHealth.onButtonDown = (button, it) => {
    if(buttonsHeld == [PAGE_HEALTH_CURRENT_HEALTH_BUTTON_APPLY])
      SetPlayerHealth(selectedPlayers, (<NumberInput>it).value);
  };
  currentHealth.AddDescription($"Press {ButtonFormatterSingle(PAGE_HEALTH_CURRENT_HEALTH_BUTTON_APPLY)}​ to apply");

  NumberInput maxHealth = new NumberInput(
    menuState: menuState, 
    text: PAGE_HEALTH_MAX_HEALTH_NAME,
    integralLength: 5,
    defaultValue: selectedPlayers.First.MaxHealth(),
    unit: ' HP'
  );
  maxHealth.onButtonDown = (button, it) => {
    foreach(Player pl! in selectedPlayers) {
      // who thought it was a good idea to use percentages when setting max health? and what's worse, percentages of original max health!?

      if(buttonsHeld == [PAGE_HEALTH_MAX_HEALTH_BUTTON_RESET] || buttonsHeld == [PAGE_HEALTH_MAX_HEALTH_BUTTON_APPLY]) {
        SetMaxHealth(pl, 100);
      }
      
      if(buttonsHeld == [PAGE_HEALTH_MAX_HEALTH_BUTTON_APPLY]) {
        MinWait();
        SetMaxHealth(pl, (<NumberInput>it).value / pl.MaxHealth() * 100);
      }
    }
  };
  maxHealth.AddDescription($"Press {ButtonFormatterSingle(PAGE_HEALTH_MAX_HEALTH_BUTTON_APPLY)}​ to apply");
  maxHealth.AddDescription($"Press {ButtonFormatterSingle(PAGE_HEALTH_MAX_HEALTH_BUTTON_RESET)}​ to reset to original value");
  maxHealth.AddDescription("Applied Max Health value might differ because of Workshop limitations", Color.Orange);
  
  Toggle autoHeal = new Toggle(
    text: "Auto Heal",
    defaultChecked: selectedPlayers[0].pageHealthAutoHeal
  );
  autoHeal.onChange = (checked, it) => {
    foreach(Player pl! in selectedPlayers)
      pl.pageHealthAutoHeal = checked;
  };

  return [currentHealth, maxHealth, new Text(), autoHeal];
}

rule: 'Player Settings/Health'
Event.OngoingPlayer
if(menuState.pageId == PAGE_HEALTH_ID)
{
  Description desc: { text: PAGE_HEALTH_DESCRIPTION_INFO, color: Color.SkyBlue, ..Description.BASE};

  menuState.CreatePage(PAGE_HEALTH_NAME, PageHealthItems(), [desc]);
}

rule: 'Draw current health info above players'
Event.OngoingPlayer
if(menuState.isOpen)
if(menuState.pageId == PAGE_HEALTH_ID)
{
  foreach(Player player in AllPlayers()) {
    if(player == EventPlayer()) continue;

    Player pl: EvaluateOnce(player);
    
    playerText.Add(pl, $"{RoundToInteger(pl.Health(), Rounding.Up)}​ / {RoundToInteger(pl.MaxHealth(), Rounding.Up)}​ HP");

    MinWait();
  }

  WaitUntil(menuState.pageId != PAGE_HEALTH_ID || !menuState.isOpen, 99999);
  
  playerText.Remove();
}

rule: 'Auto heal'
Event.OngoingPlayer
if(pageHealthAutoHeal)
{
  SetPlayerHealth(EventPlayer(), EventPlayer().MaxHealth());

  WaitUntil(EventPlayer().Health() < EventPlayer().MaxHealth(), 99999);

  Wait(0.1);
  LoopIfConditionIsTrue();
}
