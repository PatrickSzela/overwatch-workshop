import '../../../../libs/menu/components/carousel.del';
import '../../../../libs/utils/player.sortedAllPlayers.del';
import '../../_variables.del';

String PAGE_TELEPORT_ID: 'teleport';
String PAGE_TELEPORT_NAME: 'Teleport to';

String PAGE_TELEPORT_POSITION_NAME: 'Position';
String PAGE_TELEPORT_OBJECTIVE_NAME: 'Objective';
String PAGE_TELEPORT_PAYLOAD_NAME: 'Payload';
String PAGE_TELEPORT_PLAYER_NAME: 'Player';

Vector PageTeleportGetObjectivePosition(Carousel it): it.value == -2 ? PayloadPosition() : ObjectivePosition(it.value == -1 ? ObjectiveIndex() : it.value);

Component[] PageTeleportItems() {
  Component position = new Component(PAGE_TELEPORT_POSITION_NAME);
  position.AddDescription(Description.New(PickPosition.CHANGE_DISTANCE_DESCRIPTION(BUTTON_INCREASE, BUTTON_DECREASE), order: -20));
  position.onEnter = (it) => {
    pickPosition.Enable(Color.Red);
  };
  position.onLeave = (it) => {
    pickPosition.Disable();
  };
  position.onButtonDown = (button, it) => {
    if(buttonsHeld == [BUTTON_INCREASE])
      pickPosition.StartChangingRaycaseDistance(0.1);
    else if(buttonsHeld == [BUTTON_DECREASE])
      pickPosition.StartChangingRaycaseDistance(-0.1);
  };
  position.onButtonUp = (button, it) => {
    pickPosition.StopChangingRaycaseDistance();
  };
  position.SetPrimaryAction('Apply', (it) => {
    Teleport(selectedPlayers, pickPosition.position);
  });
  position.SetTertiaryAction('Add to Timeline', (it) => {
    Timeline.AddAction(timeline, ACTION_Teleport(selectedPlayers, pickPosition.position));
  });

  Carousel objective = new Carousel(
    text: PAGE_TELEPORT_OBJECTIVE_NAME,
    options: [-2, -1, 0, 1, 2, 3, 4],
    defaultValue: -1
  );
  objective.valueFormatter = (it) => ObjectiveFormatter(it.value);
  objective.SetPrimaryAction("Apply", (it) => {
    Teleport(selectedPlayers, PageTeleportGetObjectivePosition(<Carousel>it));
  });
  objective.SetTertiaryAction('Add to Timeline', (it) => {
    Timeline.AddAction(timeline, ACTION_Teleport(selectedPlayers, PageTeleportGetObjectivePosition(<Carousel>it)));
  });

  Carousel player = new Carousel(
    text: PAGE_TELEPORT_PLAYER_NAME,
    options: sortedPlayers
  );
  player.valueFormatter = (it) => PlayerWithHeroIconTeamSlotFormatter(it.value);
  player.SetPrimaryAction("Apply", (it) => {
      Teleport(selectedPlayers, (<Carousel>it).value);
  });
  player.SetTertiaryAction('Add to Timeline', (it) => {
    Timeline.AddAction(timeline, ACTION_Teleport(selectedPlayers, (<Carousel>it).value));
  });

  return [position, objective, player];
}

rule: 'Player Settings/Teleport'
Event.OngoingPlayer
if(menuState.pageId == PAGE_TELEPORT_ID)
{
  menuState.CreatePage(
    title: PAGE_TELEPORT_NAME, 
    items: PageTeleportItems()
  );

  playerInfoProps.Enable(playerInfoProps);
  playerInfoProps.EnableTransform(playerInfoProps);

  WaitUntil(menuState.pageId != PAGE_TELEPORT_NAME, 99999);

  playerInfoProps.Disable(playerInfoProps);
  playerInfoProps.DisableTransform(playerInfoProps);
}
