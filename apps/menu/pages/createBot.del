import '../_variables.del';
import '../../../libs/utils/string.formatters.del';
import '../../../libs/utils/team.availableTeams.del';
import '../../../libs/utils/hud.del';
import '../../../libs/menu/components/carousel.del';
import '../../../libs/menu/components/text.del';

String PAGE_CREATE_BOT_ID: 'createBot';
String PAGE_CREATE_BOT_NAME: 'Create Bot';

String PAGE_CREATE_BOT_HERO_ID: PAGE_CREATE_BOT_ID + 'hero';
String PAGE_CREATE_BOT_HERO_NAME: 'Hero';
String PAGE_CREATE_BOT_TEAM_ID: PAGE_CREATE_BOT_ID + 'team';
String PAGE_CREATE_BOT_TEAM_NAME: 'Team';
String PAGE_CREATE_BOT_SLOT_ID: PAGE_CREATE_BOT_ID + 'slot';
String PAGE_CREATE_BOT_SLOT_NAME: 'Slot';
String PAGE_CREATE_BOT_POSITION_ID: PAGE_CREATE_BOT_ID + 'position';
String PAGE_CREATE_BOT_POSITION_NAME: 'Position';
String PAGE_CREATE_BOT_SPAWN_ID: PAGE_CREATE_BOT_ID + 'spawn';
String PAGE_CREATE_BOT_SPAWN_NAME: 'Spawn bot';

single struct PageCreateBotProps {
  public Hero hero;
  public Team team;
  public Number slot;
  public Vector position;
}

globalvar Number createdBotsIdx = 0;

playervar PageCreateBotProps pageCreateBotProps = { hero: Hero.Reinhardt, position: Vector.Zero, slot: -1, team: Team.Team1 };
playervar Number botIdx = 0;

Component[] PageCreateBotItems() {
  // workaround for `pageCreateBotProps` containing wrong slot in deathmatch like modes even though the component shows correct information
  if(!availableTeams.Contains(pageCreateBotProps.team))
    pageCreateBotProps.team = availableTeams.First;

  Carousel hero = new Carousel(
    text: PAGE_CREATE_BOT_HERO_NAME,
    options: AllTankHeroes() + AllDamageHeroes() + AllSupportHeroes(),
    defaultValue: pageCreateBotProps.hero
  );
  hero.onChange = (value, it) => pageCreateBotProps.hero = value;
  hero.valueFormatter = (it) => HeroWithIconFormatter(it.value);

  Carousel team = new Carousel(
    text: PAGE_CREATE_BOT_TEAM_NAME,
    options: availableTeams,
    defaultValue: pageCreateBotProps.team
  );
  team.onChange = (value, it) => pageCreateBotProps.team = value;

  Carousel slot = new Carousel(
    text: PAGE_CREATE_BOT_SLOT_NAME,
    options: [-1, 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16, 17, 18, 19, 20, 21, 22, 23],
    defaultValue: pageCreateBotProps.slot
  );
  slot.valueFormatter = (it) => SlotFormatter(it.value);
  slot.onChange = (value, it) => pageCreateBotProps.slot = value;

  ComponentWithValue position = new ComponentWithValue(PAGE_CREATE_BOT_POSITION_NAME, pageCreateBotProps.position);
  
  pickPosition.EnhanceComponent(pickPosition, position);

  position.SetPrimaryAction("Choose the position", (it) => {
    pageCreateBotProps.position = pickPosition.position;
    (<ComponentWithValue>it).SetValue(pageCreateBotProps.position);
  });

  Component spawnBot = new Component(PAGE_CREATE_BOT_SPAWN_NAME);
  spawnBot.SetPrimaryAction("Spawn the bot", (it) => {
    CreateDummyBotWithMessage(pageCreateBotProps.hero, pageCreateBotProps.team, pageCreateBotProps.slot, pageCreateBotProps.position);
  });

  return [hero, team, slot, position, new Text(), spawnBot];
}

rule: 'Create Bot' 50
Event.OngoingPlayer
if(menuState.pageId == PAGE_CREATE_BOT_ID)
{
  if(!pageCreateBotProps.position)
    pageCreateBotProps.position = PickPosition.RayCast(EventPlayer());

  menuState.CreatePage(PAGE_CREATE_BOT_NAME, PageCreateBotItems());
}

rule: 'Create Bot: Create Sphere' 50
Event.OngoingPlayer
if(menuState.isOpen)
if(menuState.pageId == PAGE_CREATE_BOT_ID)
{
  pickPosition.CreateAdditionalSphere(
    color: Color.Green,
    position: pageCreateBotProps.position,
    text: $"Spawn position: {pageCreateBotProps.position}"
  );

  WaitForeverUntil(menuState.pageId != PAGE_CREATE_BOT_ID || !menuState.isOpen);

  pickPosition.DestroyAdditionalSpheres();
}

rule: 'Create Bot: Set Name'
Event.OngoingPlayer
if(IsDummyBot(EventPlayer()))
{
  // workaround for a bug that uses old hero info in `StartForcingDummyBotName`
  Wait(0.1);

  if(!botIdx) {
    createdBotsIdx += 1;
    botIdx = createdBotsIdx;
  }

  playerName = $"[BOT {botIdx}] {EventPlayer().Hero()}";

  StartForcingDummyBotName(EventPlayer(), playerName);

  WaitForeverUntil(EvaluateOnce(EventPlayer().Hero()) != EventPlayer().Hero());
  LoopIfConditionIsTrue();
}
