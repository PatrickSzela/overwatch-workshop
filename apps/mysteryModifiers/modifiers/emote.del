import '../modifier.del';
import '../../../libs/utils/player.del';
import '../../../libs/utils/button.allowAll.del';
import '../../../libs/utils/button.disallowAll.del';

String EMOTE_NAME: 'Emote';
Number EMOTE_DURATION: 5;

Modifier emote: Modifier.New(
  name: EMOTE_NAME, 
  duration: EMOTE_DURATION,
  description: ["Forces the use of an emote"]
);

rule: '[Emote] Is in air'
Event.OngoingPlayer
if(ModifierHasStarted(EMOTE_NAME))
{
  WaitUntilAlive();

  DisallowAllButtons();
  SetMoveSpeed(EventPlayer(), 0);
  SetGravity(EventPlayer(), 300);
  StartTransformingThrottle(EventPlayer(), 0.001, 0.001, FacingDirectionOf());
  CancelPrimaryAction();

  WaitForeverUntil(IsOnGround() || IsDead());

  MinWait();

  while(!IsEmoting(EventPlayer()) && IsAlive() && ModifierHasStarted(EMOTE_NAME)) {
    Communicate(EventPlayer(), Communication.EmoteUp);
    Wait(1);
  }

  WaitUntilModifierIsOverOrCondition(!IsEmoting());

  StopTransformingThrottle();
  AllowAllButtons();
  SetMoveSpeed(EventPlayer(), 100);
  SetGravity(EventPlayer(), 100);
}
