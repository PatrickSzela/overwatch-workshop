import '../modifier.del';
import '../utils/effects.del';

String HEALTH_EXCHANGE_NAME: 'Health Exchange';
Number HEALTH_EXCHANGE_DURATION: 10;

Modifier healthExchange: Modifier.New(
  name: HEALTH_EXCHANGE_NAME, 
  duration: HEALTH_EXCHANGE_DURATION,
  description: ["Dealing damage to other players heals you, and healing others damages you"]
);

rule: '[Health Exchange] Is Preloading'
Event.OngoingPlayer
if(ModifierIsPreloading(HEALTH_EXCHANGE_NAME))
{
  WaitBeforePreloading(2);

  CreatePlayerEffect(EventPlayer(), Effect.BadAura, 1, Color.Violet);
  MinWait();
  CreatePlayerEffect(EventPlayer(), Effect.ZenyattaOrbOfDiscordTargetSound, 100);
}

rule: '[Health Exchange] Heal on dealing damage'
Event.OnDamageDealt
if(ModifierHasStarted(HEALTH_EXCHANGE_NAME))
{
  if(Victim() != EventPlayer()) {
    Heal(EventPlayer(), Victim(), EventDamage() * 0.25);
  }
}

rule: '[Health Exchange] Deal damage on healing'
Event.OnHealingDealt
if(ModifierHasStarted(HEALTH_EXCHANGE_NAME))
{
  if(Healee() != EventPlayer()) {
    Damage(EventPlayer(), Healee(), EventHealing() * 0.5);
  }
}

