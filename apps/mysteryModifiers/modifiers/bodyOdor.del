import '../modifier.del';
import '../utils/effects.del';

String BODY_ODOR_NAME: 'Body Odor';
Number BODY_ODOR_DURATION: 10;
Number BODY_ODOR_RADIUS: 5;
Number BODY_ODOR_DOT: 35;

playervar Any bodyOdorDotId;

Modifier bodyOdor: Modifier.New(
  name: BODY_ODOR_NAME, 
  duration: BODY_ODOR_DURATION,
  description: ["Standing close to other players will deal damage over time to them"]
);

rule: '[Body Odor] Is Preloading'
Event.OngoingPlayer
if(ModifierIsPreloading(BODY_ODOR_NAME))
{
  WaitBeforePreloading(3);
  
  CreatePlayerEffect(EventPlayer(), Effect.Cloud, 1, CustomColor(78, 168, 9, 127), false);
  MinWait();
  CreatePlayerEffect(EventPlayer(), Effect.Ring, BODY_ODOR_RADIUS, CustomColor(78, 168, 9, 127));
  MinWait();
  CreatePlayerEffect(EventPlayer(), Effect.BadAuraSound, 100);
}

rule: '[Body Odor] Deal damage to nearby players'
Event.OngoingPlayer
if(ModifierHasStarted(BODY_ODOR_NAME))
{
  Player[] _playersNearby: PlayersWithinRadius(EventPlayer().Position(), BODY_ODOR_RADIUS, Team.All, RadiusLOS.SurfacesAndAllBarriers) - EventPlayer();
  Player[] playersNearby = _playersNearby;

  bodyOdorDotId = StartDamageOverTime(playersNearby, EventPlayer(), modifierDurationLeft, BODY_ODOR_DOT);

  WaitForeverUntil(_playersNearby != playersNearby || !ModifierHasStarted(BODY_ODOR_NAME));
  
  StopDamageOverTime(bodyOdorDotId);
  LoopIfConditionIsTrue();
}
