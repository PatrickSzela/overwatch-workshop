import '../modifier.del';
import '../utils/effects.del';

String ADAPTIVE_SHIELD_NAME: 'Adaptive Shield';
Number ADAPTIVE_SHIELD_DURATION: 7;

Number ADAPTIVE_SHIELD_HEALTH_BASE: 100;
Number ADAPTIVE_SHIELD_HEALTH_ENEMY: 100;
Number ADAPTIVE_SHIELD_RADIUS: 10;

playervar Any adaptiveShieldHealthPoolId;
playervar Number adaptiveShieldHealth = 0;

Modifier adaptiveShield: Modifier.New(
  name: ADAPTIVE_SHIELD_NAME, 
  duration: ADAPTIVE_SHIELD_DURATION,
  description: ["Creates temporary extra health that increases with more nearby enemies"]
);

rule: '[Adaptive Shield] Is Preloading'
Event.OngoingPlayer
if(ModifierIsPreloading(ADAPTIVE_SHIELD_NAME))
{
  Boolean condition: (<Player>EvaluateOnce(EventPlayer())).adaptiveShieldHealth > 0;

  WaitBeforePreloading(2);

  CreatePlayerEffect(EventPlayer(), Effect.WreckingBallAdaptiveShieldTargetEffect, 1, visibleCondition: condition);
  MinWait();
  CreatePlayerEffect(EventPlayer(), Effect.WreckingBallAdaptiveShieldTargetSound, 100, visibleCondition: condition);
}

rule: '[Adaptive Shield] Has Started'
Event.OngoingPlayer
if(ModifierHasStarted(ADAPTIVE_SHIELD_NAME))
{
  WaitUntilAlive();

  Player[] nearby: PlayersWithinRadius(PositionOf(), ADAPTIVE_SHIELD_RADIUS, OppositeTeamOf(TeamOf()), RadiusLOS.Surfaces).Filter((p) => IsAlive(p));
  
  adaptiveShieldHealth = ADAPTIVE_SHIELD_HEALTH_BASE + nearby.Length * ADAPTIVE_SHIELD_HEALTH_ENEMY;
  adaptiveShieldHealthPoolId = AddHealthPoolToPlayer(EventPlayer(), HealthType.Health, adaptiveShieldHealth, false, false); // non-recoverable shields have bugged UI

  // end
  WaitUntilIsDeadOrModifierIsOver();
  DestroyPlayerEffects();
  
  RemoveHealthPoolFromPlayer(adaptiveShieldHealthPoolId);
  adaptiveShieldHealth = 0;
  adaptiveShieldHealthPoolId = null;
}

// imagine if there was an option to check if health pool still exists....
rule: '[Adaptive Shield] On damage taken, reduce fake health to see if health pool still exists'
Event.OnDamageTaken
if(adaptiveShieldHealthPoolId)
{
  adaptiveShieldHealth -= EventDamage();
}

rule: '[Adaptive Shield] On death, reset fake health to see if health pool still exists'
Event.OnDeath
if(adaptiveShieldHealthPoolId)
{
  adaptiveShieldHealth = 0;
}