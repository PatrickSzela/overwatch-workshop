import '../modifier.del';
import '../../../libs/utils/button.allowAll.del';
import '../../../libs/utils/button.disallowAll.del';

String USE_ULTIMATE_NAME: 'Use Ultimate';
Number USE_ULTIMATE_DURATION: 10;

Modifier useUltimate: Modifier.New(
  name: USE_ULTIMATE_NAME, 
  duration: USE_ULTIMATE_DURATION,
  description: ["Fully charges and forces usage of the Ultimate ability"]
);

rule: '[Use Ultimate] Has Started'
Event.OngoingPlayer
if(ModifierHasStarted(USE_ULTIMATE_NAME))
{
  WaitUntilAlive();

  if(IsUsingUltimate()) return;

  SetUltimateCharge(EventPlayer(), 100);
  PressButton(EventPlayer(), Button.Ultimate);

  MinWait();

  while(UltimateChargePercent(EventPlayer()) == 100 && ModifierHasStarted(USE_ULTIMATE_NAME)) {
    if(!IsUsingUltimate()) {
      // if ult is cancelled, force it again
      // this has a sideeffect of bap and sym rotating their ults, but idc because blizzard didn't bother to make things consistent across different heroes
      // and there's no way im hardcoding stuff per hero again...
      PressButton(EventPlayer(), Button.Ultimate);
    } 
    PressButton(EventPlayer(), Button.PrimaryFire);
    
    Wait(0.25);
  }
}

