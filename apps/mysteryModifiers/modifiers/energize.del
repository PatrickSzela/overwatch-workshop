import '../modifier.del';
import '../utils/effects.del';
import '../../../libs/speedUpAbility/index.del';

String ENERGIZE_NAME: 'Energized';
Number ENERGIZE_DURATION: 10;

// TODO: speed up rate of fire when workshop adds that
// TODO: use Kitsune effects when are added to workshop and rename

playervar Boolean energizeEnabled = false;

Modifier energize: Modifier.New(
  name: ENERGIZE_NAME, 
  duration: ENERGIZE_DURATION,
  description: [
    "Accelerates movement speed, cooldowns, resources",
    "and Ultimate generation"
  ]
);

rule: '[Energized] Is Preloading'
Event.OngoingPlayer
if(ModifierIsPreloading(ENERGIZE_NAME))
{
  WaitBeforePreloading(2);
  
  CreatePlayerEffect(EventPlayer(), Effect.Sparkles, 2);
  MinWait();
  CreatePlayerEffect(EventPlayer(), Effect.WinstonTeslaCannonTargetSound, 100);
}

rule: '[Energized] Has Started'
Event.OngoingPlayer
if(ModifierHasStarted(ENERGIZE_NAME))
{
  SetMoveSpeed(EventPlayer(), 130);

  speedUpAbility.AllCooldowns();
  speedUpAbility.AllResources();
  speedUpAbility.UltimateCharge();

  // end
  WaitUntilModifierIsOver();
  
  speedUpAbility.StopAllCooldowns();
  speedUpAbility.StopAllResources();
  speedUpAbility.StopUltimateCharge();

  SetMoveSpeed(EventPlayer(), 100);
}