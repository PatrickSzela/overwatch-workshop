// source: https://github.com/ItsDeltin/Overwatch-Script-To-Workshop/wiki/Dictionary

struct Dictionary<K, V> {
  public K[] Keys;
  public V[] Values;

  # Returns value associated with the `key`.
  public V Get(in K key) {
    return Values[Keys.IndexOf(key)];
  }

  # Checks whether `key` exists in the dictionary.
  public Boolean Contains(in K key) {
    return Keys.Contains(key);
  }
  
  # Sets a `value` in the `dictionary` at `key`.
  public ref void Set(in K key, in V value) {
    Values[Keys.IndexOf(key)] = value;
  }
  
  # Adds a `value` to the `dictionary` at `key`.
  public ref void Add(in K key, in V value) {
    Keys[Keys.Length] = key;
    Values[Values.Length] = value;
  }

  # Sets a `value` in the `dictionary` at `key`. Will add the `key` if it doesn't exist.
  public ref void SetOrAdd(in K key, in V value) {
    Number index! = Keys.IndexOf(key);
    if (index == -1) // Key not found; add it.
      Add(key, value);
    else
      Values[index] = value;
  }
}
