import './customButtons.del';
import '../utils/dictionary.del';
import '../utils/string.formatters.del';
import '../utils/button.allButtons.del';
import './menu.del';
import './_rules.del';
import './components/component.del';
import './components/text.del';

// TODO: make `PuppetMenuState`?

Color COLOR_NEUTRAL: Color.White;
Color COLOR_INFO: Color.SkyBlue;
Color COLOR_WARNING: Color.Orange;
Color COLOR_ERROR: Color.Red;
Color COLOR_SWITCH: Color.Green;
Color COLOR_DISABLED: Color.Gray;

single struct RenderedText {
  public Color color;
  public String text;
}

single struct HistoryItem {
  public String id;
  public Number outerScroll;
  public Number innerScroll;

  public static HistoryItem BASE: { id: '', innerScroll: 0, outerScroll: 0 };
}

struct RenderedTexts {
  public RenderedText[] titles;
  public RenderedText[] separators;
  public RenderedText[] items;
  public RenderedText[] descriptions;

  public RenderedText[] merged;

  public static RenderedTexts BASE: {
    titles: [], separators: [], items: [], descriptions: [], merged: []
  };
}

# Holds current state of `Menu` for a player and handles logic for displaying and controlling every `Component` and its derivatives.
class MenuState {
  # Maximum number of visible items.
  public static Number MAX_ITEMS: Menu.MAX_ITEMS;

  # Whether player has access to menu.
  public Boolean isEnabled;
  # Whether menu is currently open.
  public Boolean isOpen;
  # Whether player can navigate the menu. Items will still receive navigation events.
  public Boolean isLocked;
  # Whether opening and closing menu logic is handled from outside.
  public Boolean isPuppet;
  # Whether this menu is currently used menu.
  public Boolean isFocused;
  # Whether menu is currently in refresh stage.
  public Boolean isRefreshing: pageId == '<REFRESH>';
  # Whether the text explaining how to toggle menu should be hidden when menu is closed.
  public Boolean hideTextWhenClosed;
  # Whether menu should rerender.
  public Boolean shouldRerender;
  # Combination of buttons used to toggle menu on and off.
  public Button[] _toggleMenu;
  # ID of root page.
  public String _rootPageId;
  # List of available items on current page.
  public Component[] items;
  # Index of currently hovered item in `items`.
  public Number hoveredItemIdx;
  # List of currently visible items.
  public Component[] visibleItems: items.Slice(history.Last.outerScroll, MAX_ITEMS);
  # List of components that aren't disabled (can be hovered).
  public Component[] enabledItems: items.FilteredArray((i, idx) => !i.disabled);
  # Currently hovered item.
  public Component hoveredItem: items[hoveredItemIdx];
  # Stack of IDs of visited pages.
  public HistoryItem[] history;
  # ID of currently open page.
  public String pageId: history.Last.id;
  # List of rendered items.
  public RenderedTexts renderedTexts;
  # List of additional descriptions that will be show on current page.
  public Description[] descriptionsPage;
  # List of additional descriptions that will be shown until manually removed.
  public Description[] descriptionsMenu;
  # Component that will be displayed when there are no items in menu
  protected Component emptyText;

  public constructor(in String rootPageId, in Button[] toggleMenu, in Boolean enabled = true, in Boolean puppet = false) {
    _toggleMenu = toggleMenu;
    _rootPageId = rootPageId;

    if(rootPageId != null)
      history = [{ id: rootPageId, ..HistoryItem.BASE }];
    else 
      history = [];
    
    items = [];
    isLocked = false;
    isFocused = false;
    isEnabled = enabled;
    isPuppet = puppet;
    hoveredItemIdx = 0;

    renderedTexts = RenderedTexts.BASE;
    emptyText = new Text("It's a bit empty in here...");

    // render closed title
    MarkToRerender();
  }

  // helpers
  public Boolean HistoryContains(in String pageId): 
    history.Map((i) => i.id).Contains(pageId);

  public void SetTitle(in String title) {
    RenderTitle(title);
  }

  # Forcefully rerenders currently hovered item. Call after manually adding description.
  public void RerenderHoveredItem() "[MenuState] Rerender Hovered Item" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: RerenderHoveredItem');
    // DisableInspectorRecording();

    hoveredItem.MarkToRerender();
    MarkToRerender();
  }

  # Forcefully rerenders specified item. Call after manually adding description.
  public void RerenderItem(Component item!) "[MenuState] Rerender Item" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: RerenderItem');
    // DisableInspectorRecording();

    item.MarkToRerender();
    MarkToRerender();
  }

  # Forcefully rerenders all visible items.
  public void RerenderAllItems() "[MenuState] Rerender All Items" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: RerenderAllItems');
    // DisableInspectorRecording();

    foreach(Component item! in visibleItems)
      item.MarkToRerender();
    
    MarkToRerender();
  }
  
  public void SetEnabled(Boolean enabled!) "[MenuState] Set Enabled" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: SetEnabled');
    // DisableInspectorRecording();

    if(enabled != isEnabled) {
      Close();
      isEnabled = enabled;
    }
  }

  public void SetFocused() "[MenuState] Set Focused" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: SetFocused');
    // DisableInspectorRecording();

    if(currentMenuState != this) {
      currentMenuState.isFocused = false;

      if(currentMenuState.isOpen) {
        currentMenuState.hoveredItem.OnLeave();
      }
      
      currentMenuState = this;
      isFocused = true;

      if(isOpen) {
        hoveredItem.OnEnter();
      }
    }
  }

  # NOTE: This won't rerender the menu
  public void SetHoveredItemIdx(Number index!) "[MenuState] Set Hovered Item Index" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: SetHoveredItemIdx');
    // DisableInspectorRecording();

    history[history.Length - 1] = { innerScroll: index, outerScroll: 0, ..history.Last };

    HandleScroll();
  }

  public void AddItems(Component[] items!, Boolean callOnItemsChanged! = true) "[MenuState] Add Items" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: AddItems');
    // DisableInspectorRecording();

    this.items += items;

    if(callOnItemsChanged) {
      OnItemsChanged();
    }
  }

  public void RemoveItems(Component[] items!, Boolean callOnItemsChanged! = true) "[MenuState] Remove Items" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: RemoveItems');
    // DisableInspectorRecording();

    this.items -= items;
    
    foreach(Component item! in items) {
      if(item == emptyText) continue;

      if(item.child) {
        delete(item.child);
      }
      delete(item);
    }
    
    if(callOnItemsChanged) {
      OnItemsChanged();
    }
  }

  public void ReplaceItems(Component[] items!) "[MenuState] Replace Items" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: ReplaceItems');
    // DisableInspectorRecording();

    RemoveItems(this.items - items, false);
    AddItems(items - this.items, false);

    // keep the order of passed items
    this.items = items;

    OnItemsChanged();
  }

  protected void OnItemsChanged() "[MenuState] On Items Changed" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: OnItemsChanged');
    // DisableInspectorRecording();

    if(!items) {
      items = [emptyText];
    } else if(items.Length > 1 && items.Contains(emptyText)) {
      items -= emptyText;
    }
    
    // hoveredItemIdx is calculated later in `HandleScroll()`
    Number scroll! = history.Last.outerScroll + history.Last.innerScroll;
    Component hover: items[scroll];

    if(!hover || hover.disabled) {
      Component[] enabled! = enabledItems;
      history[history.Length - 1].innerScroll = Max(items.IndexOf(enabled.First), Min(items.IndexOf(enabled.Last), scroll));
    }

    HandleScroll();
  }

  // phase 1: handle events
  public void Open() "[MenuState] Open" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: Open');
    // DisableInspectorRecording();

    if(!isOpen && isEnabled) {
      isOpen = true;

      if(isFocused) hoveredItem.OnEnter();
      MarkToRerender();
    }
  }

  public void Close() "[MenuState] Close" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: Close');
    // DisableInspectorRecording();

    if(isOpen) {
      isOpen = false;
      
      if(isFocused) hoveredItem.OnLeave();
      MarkToRerender();
    }
  }

  public void CreatePage(String title!, Component[] items!, Description[] descriptions! = []) "[MenuState] Create Page" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: CreatePage');
    // DisableInspectorRecording();

    SetTitle(title);
    AddItems(items);

    foreach(Description desc! in descriptions)
      descriptionsPage += desc;

    if(isOpen) {
      hoveredItem.OnEnter();
      // `AddItems` already adds items to render queue
      MarkToRerender();
    }
  }

  public void DestroyPage() "[MenuState] Destroy Page" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: DestroyPage');
    // DisableInspectorRecording();

    if(isOpen) {
      hoveredItem.OnLeave();
    }

    RemoveItems(items, false);

    buttonsHeld = [];
    descriptionsPage = [];
  }

  public virtual void Navigate(Vector direction!) "[MenuState] Navigate" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: Navigate');
    // DisableInspectorRecording();

    hoveredItem.OnNavigate(direction);

    if(!isLocked && (direction == Down() || direction == Up())) {
      Number offset! = 0;
      Number hov: hoveredItemIdx;
      Component[] it! = enabledItems;

      if(direction == Down()) {
        Number nextItemIdx! = items.IndexOf(items.FilteredArray((i, idx) => !i.disabled && idx > hov).First);

        offset = (nextItemIdx == -1 ? items.IndexOf(it.First) : nextItemIdx) - hov;
      } else if(direction == Up()) {
        Number prevItemIdx! = items.IndexOf(items.FilteredArray((i, idx) => !i.disabled && idx < hov).Last);

        offset = (prevItemIdx == -1 ? items.IndexOf(it.Last) : prevItemIdx) - hov;
      }

      if(offset != 0) {
        hoveredItem.OnLeave();
        history[history.Length - 1].innerScroll += offset;
        HandleScroll(); 
        hoveredItem.OnEnter();
      }
    }

    MarkToRerender();
  }

  public virtual void ButtonDown(Button button!) "[MenuState] Button Down" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: ButtonDown');
    // DisableInspectorRecording();

    if(!isOpen) {
      if(buttonsHeld == _toggleMenu && !isPuppet) {
        Open();
      }
    } else {
      hoveredItem.OnButtonDown(button);

      if(!isLocked) {
        if(buttonsHeld == _toggleMenu && !isPuppet) {
          Close();
        } else if(buttonsHeld == [BUTTON_BACK]) {
          GoBack();
        }
      }
    }

    if(isOpen) {
      MarkToRerender();
    }
  }

  public virtual void ButtonUp(Button button!) "[MenuState] Button Up" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: ButtonUp');
    // DisableInspectorRecording();

    // when changing page `buttonsHeld` is being cleared
    if(!buttonsHeld.Contains(button)) return;

    if(isOpen) {
      hoveredItem.OnButtonUp(button);
      MarkToRerender();
    }
  }

  // phase 2: render
  protected void RenderClosedItems() {
    renderedTexts.merged = isPuppet || hideTextWhenClosed ? [] : [
      { 
        text: $"Press {ButtonFormatter_Underscores(_toggleMenu)}​ to toggle Menu", 
        color: COLOR_NEUTRAL
      }
    ];
  }

  protected void RenderTitle(in String title) {
    renderedTexts.titles = [
      { 
        text: title,
        color: COLOR_NEUTRAL
      }
    ];
  }

  protected void RenderSeparators() {
    renderedTexts.separators = [
      { 
        text: $"──────────────────────{items.Length < MAX_ITEMS ? '─' : history.Last.outerScroll == 0 ? '△' : '▲'}──────────────────────", 
        color: COLOR_NEUTRAL 
      },
      {
        text: $"──────────────────────{items.Length < MAX_ITEMS ? '─' : history.Last.outerScroll == items.Length - MAX_ITEMS ? '▽' : '▼'}──────────────────────",
        color: COLOR_NEUTRAL
      }
    ];
  }

  protected void RenderDescription() {
    Description[] descriptionArr = hoveredItem.descriptions;

    if(hoveredItem.child) {
      descriptionArr += hoveredItem.child.descriptions;
    }

    descriptionArr += descriptionsPage + descriptionsMenu;

    renderedTexts.descriptions = descriptionArr
      .FilteredArray((i) => i.visible)
      .SortedArray((i) => i.order)
      .Map((i) => <RenderedText>{text: i[0], color: i[1]});
  }

  protected void RenderVisibleItems() {
    RenderDescription();

    Component[] v = visibleItems;

    renderedTexts.items = [];

    for(Number idx = 0; idx < v.Length; idx++) {
      Number actualIdx = idx + history.Last.outerScroll;
      Component item = v[idx];
      
      if(item.shouldRerender || item.child.shouldRerender) {
        // EnableInspectorRecording();
        // LogToInspector($'Rerendering: {item.text}');
        // DisableInspectorRecording();

        // `Render()` of a child called here, not in Component because it becomes recursive for some reason
        if(item.child) {
          item.child.Render();
          item.child.shouldRerender = false;
        }

        item.Render();
        item.shouldRerender = false;
      }

      renderedTexts.items[idx] = { 
        text: $"{!item.disabled ? isFocused && hoveredItemIdx == actualIdx ? '● ' : '○ ' : ''}{item.renderedText}",
        color: item.color 
      };
    }
  }

  protected void MergeTexts() {
    renderedTexts.merged = renderedTexts.titles + [renderedTexts.separators[0]] + renderedTexts.items;

    if(items.Length > MAX_ITEMS || renderedTexts.descriptions.Length)
      renderedTexts.merged += [renderedTexts.separators[1]];

    renderedTexts.merged += renderedTexts.descriptions;
  }

  public void MarkToRerender() {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: Mark to rerender');
    // DisableInspectorRecording();

    shouldRerender = true;
  }

  public void Render() "[MenuState] Render" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: Render');
    // DisableInspectorRecording();

    shouldRerender = false;

    if(isOpen) {
      RenderVisibleItems();
      MergeTexts();
    } else {
      RenderClosedItems();
    }
  }

  public void RULE_Render() {
    WaitForeverUntil(shouldRerender);
    Render();
    Loop();
  }

  protected void HandleScroll() "[MenuState] Handle Scroll" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: HandleScroll');
    // DisableInspectorRecording();

    Number idx! = history.Length - 1;
    Number offset! = 0;

    if(history.Last.innerScroll > MAX_ITEMS - 1) {
      offset = history.Last.innerScroll - (MAX_ITEMS - 1);
      history[idx] = {innerScroll: MAX_ITEMS - 1, outerScroll: history.Last.outerScroll + offset, ..history[idx]};
    } else if(history.Last.innerScroll < 0) {
      offset = history.Last.innerScroll;
      history[idx] = {innerScroll: 0, outerScroll: Max(0, history.Last.outerScroll + offset), ..history[idx]};
    }

    hoveredItemIdx = history.Last.innerScroll + history.Last.outerScroll;
    RenderSeparators();
  }

  public void Refresh() "[MenuState] Refresh" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: Refresh');
    // DisableInspectorRecording();

    history += [{ id: '<REFRESH>', innerScroll: 0, outerScroll: 0 }];
    Wait();
    DestroyPage();
    history.ModRemoveByIndex(history.Length - 1);
  }

  public virtual void GoTo(String pageId!, Boolean historyReplace! = false) "[MenuState] Go To" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: GoTo');
    // DisableInspectorRecording();

    if(this.pageId != pageId) {
      DestroyPage();

      if(historyReplace)
        history[history.Length - 1] = { id: pageId, ..HistoryItem.BASE };
      else
        history += { id: pageId, ..HistoryItem.BASE };
    }
  }

  public virtual void GoBack() "[MenuState] Go Back" {
    // EnableInspectorRecording();
    // LogToInspector($'{EventPlayer()}: GoBack');
    // DisableInspectorRecording();

    if(history.Length == 1) {
      if(!isPuppet) Close();
      return;
    }

    DestroyPage();
    history.ModRemoveByIndex(history.Length - 1);
  }

  public Number AddPageDescription(Description desc!) "[MenuState] Add Page Description" {
    descriptionsPage += desc;
    return descriptionsPage.Length - 1;
  }

  public Number AddMenuDescription(Description desc!) "[MenuState] Add Menu Description" {
    descriptionsMenu += desc;
    return descriptionsMenu.Length - 1;
  }

  public void RemoveMenuDescription(Number idx!) "[MenuState] Remove Page Description" {
    descriptionsMenu.ModRemoveByIndex(idx);
  }

  public void WaitUntilButtonDown(in String id, in Button button) {
    WaitForeverUntil(onButtonDown(id, button));
  }

  public void WaitUntilButtonUp(in String id, in Button button) {
    WaitForeverUntil(!onButtonDown(id, button));
  }

  // events
  public Boolean onButtonDown(in String id, in Button button): isOpen && isFocused && hoveredItem.id == id && buttonsHeld.Contains(button);
   
  public Boolean onEnter(in String id): isOpen && isFocused && hoveredItem.id == id;

  public Boolean onNavigate(in String id, in Vector direction) {
    Vector diff = ThrottleOf(EventPlayer()) - direction * 0.5;
    Boolean x = AbsoluteValue(diff.X) > 0.5;
    Boolean y = AbsoluteValue(diff.Y) > 0.5;
    return isOpen && isFocused && hoveredItem.id == id && (x && !y || !x && y);
  }
}