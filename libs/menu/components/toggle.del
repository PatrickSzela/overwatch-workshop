import '../../utils/string.formatters.del';
import './componentWithValue.del';

class Toggle: ComponentWithValue 
{
  public static Button BUTTON_TOGGLE: Button.Ability2;
  public Boolean checked: value;
  public Boolean defaultChecked: defaultValue;


  public constructor(
    in String text,
    in Boolean defaultChecked = null,
    in Component child = null
  ) {
    ToggleConstructor(text, defaultChecked);
  }

  protected void ToggleConstructor(
    String text!,
    Boolean defaultChecked! = null,
    Component child! = null
  ) "[Toggle] Constructor" {
    ComponentWithValueConstructor(text, defaultChecked, child);

    this.valueFormatter = (it) => BooleanFormatter(it.value);

    AddDescription(Description.New($"Press {ButtonFormatterSingle(BUTTON_TOGGLE)}â€‹ to toggle"));
  }

  public virtual void SetChecked(Boolean checked!) "[Toggle] Set Checked" {    
    if(this.checked != checked) {
      value = checked;
      OnChange();
    }
  }

  // events
  public override void OnButtonDown(Button button!) "[Toggle] OnButtonDown" {
    onButtonDown(button, this);

    if(button == BUTTON_TOGGLE)
      SetChecked(!checked);

    if(child != null)
      child.OnButtonDown(button);
  }
}