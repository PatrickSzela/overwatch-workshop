import './componentWithValue.del';

class Carousel: ComponentWithValue
{
  public Any[] options;

  public constructor(
    in String text,
    in Any[] options,
    in Any defaultValue = null
  ) {
    CarouselConstructor(text, options, defaultValue);
  }

  protected void CarouselConstructor(
    String text!,
    Any[] options!,
    Any defaultValue! = null
  ) "[Carousel] Constructor" {
    this.defaultValue = !options.Contains(defaultValue) ? options.First : defaultValue;
    this.options = options;
    
    ComponentWithValueConstructor(text, this.defaultValue, null);

    AddDescription(Description.New('Use [←] or [→] to select an option'));
  }

  public override void OnNavigate(Vector direction!) "[Carousel] OnNavigate" {
    onNavigate(direction, this);

    Any old! = value;
    Number index! = options.IndexOf(value);

    if(index == -1) {
      index = 0;
    } else {
      if(direction == Right()) index--;
      else if(direction == Left()) index++;

      if(index < 0) index = options.Length - 1;
      else if(index > options.Length - 1) index = 0;
    }

    value = options[index];

    if(old != options[index]) {
      OnChange();
    }
  }

  public override String Render(String childText! = null) "[Carousel] Render" {
    onRender(this);
    if(options.Length == 0) return $"{text}: Empty";
    else return $"{text}: ← {valueFormatter(this)}​ →";
  }
}