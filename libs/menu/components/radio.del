import '../menuState.del';
import './toggle.del';

class Radio: Toggle
{
  public constructor(
    in MenuState menuState,
    in String text, 
    in String name,
    in Boolean defaultChecked = null,
    in Component child = null
  ) {
    RadioConstructor(menuState, text, name, defaultChecked, child);
  }

  protected void RadioConstructor(
    MenuState menuState!,
    String text!, 
    String name!,
    Boolean defaultChecked! = null,
    Component child! = null
  ) "[Radio] Constructor" {
    ToggleConstructor(text, defaultChecked, child);

    this.name = name;
    this._menuState = menuState;
  }

  public override void SetChecked(Boolean checked!) "[Radio] Set Checked" {
    if(this.checked != checked) {
      if(checked) {
        Component[] arr! = _menuState.items.Map((i) => i.name == name ? i : i.child.name == name ? i : null).FilteredArray((i) => i);

        foreach(Component sibling! in arr) {
          if((<Toggle>sibling).checked) {
            (<Toggle>sibling).value = false;
            (<Toggle>sibling).shouldRerender = true;
          }
        }
      }

      value = checked;
      OnChange();
    }
  }

  public override String Render(String childText! = null) "[Radio] Render" {
    onRender(this);
    return $"{checked ? "(✓)" : "(    )"}​ {childText || text}";
  }
}